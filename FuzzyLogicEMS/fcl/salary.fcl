FUNCTION_BLOCK getSalary	// Maps to a method

    VAR_INPUT	// input params to a method
        screen : REAL;
        experience : REAL;
        qualifications : REAL;
        iq : REAL;
    END_VAR

    VAR_OUTPUT  // return type from method
        salary : REAL;
    END_VAR

     FUZZIFY iq
        TERM low := gbell 10 30 80;
        TERM average := gbell 10 30 100;
        TERM high := gbell 10 30 120;
    END_FUZZIFY

   FUZZIFY qualifications
        TERM basic := (0,1) (2,1) (6,0);
        TERM moderate := trape 4 5 7 8;
        TERM excellent := (4,0) (6,0) (8,1);
    END_FUZZIFY

   FUZZIFY experience
        TERM junior := (0,1) (2,1) (3,0);
        TERM midLevel := trape 2 3 5 6;
        TERM expert := (5,0) (6,1) (8,1);
    END_FUZZIFY

     FUZZIFY screen
        TERM clueless := trian 0 2.5 5;
        TERM good := trian 2.5 5 7.5;
        TERM excellent := trian 5 7.5 10;
    END_FUZZIFY

    DEFUZZIFY salary // method body
    	TERM low := (0,1) (20,0);
    	TERM average :=  trape 15 16 19 20;
    	TERM high := (25,0) (35,1);
    	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
    	DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
    END_DEFUZZIFY

    // Inference rules
    RULEBLOCK No1
    	AND : MIN;	// Use 'min' for 'and'
    	ACT : MIN;	// Use 'min' activation method
    	ACCU : MAX;	// Use 'max' accumulation method

    	RULE 1 : IF iq IS low AND qualifications IS basic THEN salary IS low;
    	RULE 2 : IF experience IS expert OR screen is excellent THEN salary IS high;
    	RULE 3 : IF qualifications IS moderate AND screen IS good THEN salary IS average;
    END_RULEBLOCK

END_FUNCTION_BLOCK